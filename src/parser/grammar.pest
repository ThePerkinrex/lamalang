pub_kw = {"pub"}
fn_kw = {"fn"}
trait_kw = {"trait"}
struct_kw = {"struct"}
type_kw = {"type"}
impl_kw = {"impl"}
for_kw = {"for"}
// TODO Add modules and scoped types & traits (Self::Target)

ident = @{(ASCII_ALPHA | "_")~(ASCII_ALPHANUMERIC | "_")*}
type_name = @{ident}
generic_type = {type_name  ~ ("<" ~ generics? ~ ">")?}
trait_name = @{type_name}
generic_trait = {type_name  ~ ("<" ~ generics? ~ ">")?}

generics = {generic_type ~ ("," ~ generic_type)* ~ ","?}
where_clause = {"where" ~ (generic_bound ~ ("," ~ generic_bound)*) ~ ","?}
	generic_bound = {generic_type ~ ":" ~ generic_trait ~ ("+" ~ generic_trait)*}
fn_arguments = {(fn_arg ~ ("," ~ fn_arg)* ~ ","?)?}
	fn_arg = {ident ~ ":" ~ generic_type}
fn_return = {("->" ~ generic_type)?}
block = {"{" ~ non_returning_statement* ~ statement? ~ "}"}
	non_returning_statement = {expr ~ ";"}
	statement = {expr}

item = _{fn_item | trait_item | struct_item | impl_trait_item | impl_item}
	fn_item = {pub_kw? ~ fn_kw ~ ident ~ ("<" ~ generics? ~ ">")? ~ "(" ~ fn_arguments ~ ")" ~ fn_return ~ where_clause? ~ block}
	trait_item = {pub_kw? ~ trait_kw ~ trait_name ~ ("<" ~ generics? ~ ">")? ~ where_clause? ~ "{" ~ (trait_type | trait_fn)* ~ "}"}
		trait_type = {type_kw ~ generic_type ~ (":" ~ generic_trait ~ ("+" ~ generic_trait)*)? ~ ";"}
		trait_fn = {fn_kw ~ ident ~ ("<" ~ generics? ~ ">")? ~ "(" ~ fn_arguments ~ ")" ~ fn_return ~ where_clause? ~ (block | ";")}
	struct_item = {pub_kw? ~ struct_kw ~ type_name ~ ("<" ~ generics? ~ ">")? ~ where_clause? ~ "{" ~ fn_arguments ~ "}"}
	impl_trait_item = {impl_kw ~ ("<" ~ generics? ~ ">")? ~ generic_trait ~ for_kw ~ generic_type ~ where_clause? ~ "{" ~ (impl_type | impl_fn)* ~ "}"}
	impl_item = {impl_kw ~ ("<" ~ generics? ~ ">")? ~ generic_type ~ where_clause? ~ "{" ~ (impl_type | impl_fn)* ~ "}"}
		impl_type = {type_kw ~ generic_type ~ "=" ~ generic_type ~ ";"}
		impl_fn = {fn_kw ~ ident ~ ("<" ~ generics? ~ ">")? ~ "(" ~ fn_arguments ~ ")" ~ fn_return ~ where_clause? ~ block}

module = _{SOI ~ (item)* ~ EOI}


value = { if_statement | num | string | ident }
	num = @{ int ~ ("." ~ ASCII_DIGIT*)? ~ (^"e" ~ int)? }
		int = { ("+" | "-")? ~ ASCII_DIGIT+ }
	string = _{ "\"" ~ string_content ~ "\"" }
		string_content = @{("\\\"" | (!"\"" ~ ANY))*}
	if_statement = {"if" ~ expr ~ block ~ elseif_clause* ~ else_clause?}
		elseif_clause = {"else if" ~ expr ~ block}
		else_clause = {"else" ~ block}
		

operation = _{ add | subtract | multiply | divide | power }
	add      = { "+" }
	subtract = { "-" }
	multiply = { "*" }
	divide   = { "/" }
	power    = { "^" }
unary = {not}
	// negative = {"-"}
	not = {"!"}

expr = {(term ~ (operation ~ term)*)}
term = {unary* ~ ("(" ~ expr ~ ")" | value) ~ fn_call*}
	fn_call = { "(" ~ (expr ~ ("," ~ expr)* ~ ","?)? ~ ")"}

calculation = _{ SOI ~ expr ~ EOI }

WHITESPACE = _{ " " | "\t" | NEWLINE }